grammar com.ino9dev.Ltml with org.eclipse.xtext.common.Terminals
generate ltml "http://www.ino9dev.com/Ltml"

Model:
    statements+=Statement*;
    
Statement:
    Manifest
    |LoadTest
    |LoadGroup
    |LoadGenerator
    |Script
    |SL_COMMENT
;

Manifest:
    ("Manifest"|"Mn")
    "{"
        "Id" name=ID
        ("Name" manifestname=STRING)?
        ("Version"|"ver") version=STRING
        ("InstanceType" instancetype=InstanceType)?
        ("ModelInstancedPath" modelinstancedpath=STRING)?
        ("Corpname" corpname=STRING)?
    "}"
;

LoadTest:
    ("LoadTest"|"Lt")
    "{"
        "Id" name=ID
        ("Name" loadtestname=STRING)?
        ("Purpose" purpose=STRING)?
        //per loadtest schedule
        "LoadGroups" loadgroups+=[LoadGroup](',' loadgroups+=[LoadGroup])*
        (schedule=Schedule)?
        report=Report
    "}"
;

LoadGroup:
    ("LoadGroup"|"Lgrp")
    "{"
        "Id" name=ID
        ("Name" loadgroupname=STRING)?
        ("ConccurentCount"|"Cc") cc=INT
        "Script" script=[Script]
        "Iteration" iteration=("INFINITY"|STRING)
        "LoadGenerator" loadgenerator=[LoadGenerator]
        "RampUp" rampup=STRING
        // per loadgroup schedule
        (schedule=Schedule)?
    "}"
;

LoadGenerator:
    ("LoadGenerator"|"Lgen")
    "{"
        "Id" name=ID
        ("Name" loadgeneratorname=STRING)?
        "TargetIp" ip=STRING //todo IPADDR
        "TargetPort" port=STRING //todo PORT
        "Location" location=STRING
        "AuthUsername" username=STRING
        "AuthPassword" password=STRING
    "}"
;

Schedule:
    "Schedule"
    "{"
        (
        "Start" start=STRING
        "End" end=STRING
        |
        "Duration" duration=INT
        "Delay" delay=INT
        )
    "}"
;

Script:
    ("Script"|"Sc")
    "{"
        "Id" name=ID
        "Name" scriptname=STRING
        ("Transactions"|"Trs") 
        "{"
            (transactions+=Transaction)*
        "}"
    "}"
;

Transaction:
    ("Transaction" | "Tr")
    "{"
        "Id" name=ID
        "Name" transactionanme=STRING
        "Protocol" protocol=Protocol
        "Method" method=Method
        "Server" server=(STRING|IPADDRESS)
        "Path" path=STRING
        ("RequestParameters" "["
            param+=Param*
         "]"|"Body" body=STRING
        )?
        ("CaptureFileName" capturefilename=STRING)?
        ("ResponseExpected" text=STRING ("Exist"|"NotExist"|"Included"))?
    "}"
;


terminal IPADDRESS:
    ('1'..'2''0'..'5''0'..'5'
        |'1'..'9''0'..'9'
        |'0'..'9')
    '.'
    ('1'..'2''0'..'5''0'..'5'
        |'1'..'9''0'..'9'
        |'0'..'9')
    '.'
    ('1'..'2''0'..'5''0'..'5'
        |'1'..'9''0'..'9'
        |'0'..'9')
    '.'
    ('1'..'2''0'..'5''0'..'5'
        |'1'..'9''0'..'9'
        |'0'..'9')
;

Report:
    "Report"
    "{"
        (noreport?="NoReport"|
            (summary?="Summary"
             ("Result" resultpath=STRING)?)
             (hps?="HitPerSecond"
              |tps?="TransactionPerSecond"
              |resptime?="ResponseTime"
              |cc?="ConccurentCount")*
        )
    "}"
;

Param:
    key=STRING"="value=STRING
;

enum Method:
    GET="GET"
    |POST="POST"
    |DELETE="DELETE"
    |PUT="PUT"
    |OPTION="OPTION"
;

enum InstanceType:
    JMETER="JMeter"
    |LOADRUNNER="LoadRunner"
;

enum Protocol:
    HTTP="HTTP"
    |HTTPS="HTTPS"
    |SMTP="SMTP"
    |FTP="FTP"
    //others when if those are needed
;
